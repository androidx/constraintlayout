plugins {
    id 'com.android.application'
    id 'kotlin-android'
}

android {
    compileSdkVersion compile_target_sdk_version

    defaultConfig {
        applicationId "androidx.constaitnlayout.compose.app"
        minSdkVersion 29
        targetSdkVersion compile_target_sdk_version
        versionCode 1
        versionName "1.0"

        testInstrumentationRunner "androidx.test.runner.AndroidJUnitRunner"
    }

    buildTypes {
        release {
            minifyEnabled false
            proguardFiles getDefaultProguardFile('proguard-android-optimize.txt'), 'proguard-rules.pro'
        }
    }
    compileOptions {
        sourceCompatibility JavaVersion.VERSION_1_8
        targetCompatibility JavaVersion.VERSION_1_8
    }

    buildFeatures {
        compose true
        buildConfig false
    }

    packagingOptions {
        exclude 'META-INF/AL2.0'
        exclude 'META-INF/LGPL2.1'
    }

    kotlinOptions {
        jvmTarget = JavaVersion.VERSION_1_8.toString()
    }

    composeOptions {
        kotlinCompilerVersion kotlin_version
        kotlinCompilerExtensionVersion compose_compiler_version
    }
}

dependencies {
    implementation project(':core')
    implementation project(':compose')
    implementation project(':tools')
    implementation project(path: ':constraintlayout')
//    implementation "androidx.constraintlayout:constraintlayout-compose:1.0.0-alpha08"

    implementation "androidx.activity:activity-compose:1.3.0"

    implementation "androidx.compose.material:material:$compose_version"
    implementation "androidx.compose.material:material-icons-extended:$compose_version"
    implementation "androidx.compose.ui:ui:$compose_version"
    implementation "androidx.compose.ui:ui-unit:$compose_version"
    implementation "androidx.compose.foundation:foundation-layout:$compose_version"
    implementation "androidx.compose.foundation:foundation:$compose_version"
    implementation("androidx.compose.ui:ui-tooling:$compose_version")

    implementation "com.google.accompanist:accompanist-coil:0.14.0"

    implementation "org.jetbrains.kotlin:kotlin-stdlib:$kotlin_version"
    implementation 'androidx.core:core-ktx:1.6.0'
    implementation 'androidx.appcompat:appcompat:1.3.0'
    implementation 'com.google.android.material:material:1.4.0'
    testImplementation 'junit:junit:4.13.1'

    androidTestImplementation 'androidx.test:runner:1.3.0'
}

configurations.all {
    resolutionStrategy.dependencySubstitution {
        substitute module("androidx.constraintlayout:constraintlayout-core") using project(":core") because "we work with the unreleased development version"
        substitute module("androidx.constraintlayout:constraintlayout") using project(":constraintlayout") because "we work with the unreleased development version"
    }
}

tasks.withType(org.jetbrains.kotlin.gradle.tasks.KotlinCompile).configureEach {
    kotlinOptions {
        freeCompilerArgs += "-Xopt-in=androidx.constraintlayout.compose.ExperimentalMotionApi"
        freeCompilerArgs += "-Xopt-in=androidx.compose.material.ExperimentalMaterialApi"
    }
}
//configurations.configureEach {
//    resolutionStrategy.eachDependency { DependencyResolveDetails details ->
//        if (details.requested.group == 'androidx.lifecycle' &&
//                details.requested.name != 'lifecycle-viewmodel-compose') {
//            details.useVersion('2.3.0-rc01')
//        }
//        if (details.requested.group == 'androidx.savedstate') {
//            details.useVersion('1.1.0-rc01')
//        }
//    }
//}