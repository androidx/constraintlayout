plugins {
    id 'androidx.build.publishing'
    id 'com.android.library'
    id 'org.jetbrains.kotlin.android'
}

def compose_version = '1.0.0-rc02'
def kotlin_version = "1.5.10"

android {
    compileSdkVersion 30
    useLibrary 'android.test.runner'
    useLibrary 'android.test.base'

    defaultConfig {
        minSdkVersion 21
        testInstrumentationRunner "androidx.test.runner.AndroidJUnitRunner"
        targetSdkVersion 30
    }

    compileOptions {
        sourceCompatibility JavaVersion.VERSION_1_8
        targetCompatibility JavaVersion.VERSION_1_8
    }


    buildFeatures {
        compose true
        buildConfig false
    }

    kotlinOptions {
        jvmTarget = JavaVersion.VERSION_1_8.toString()
        useIR = true
    }

    composeOptions {
        kotlinCompilerVersion kotlin_version
        kotlinCompilerExtensionVersion compose_version
    }

    packagingOptions {
      exclude 'META-INF/AL2.0'
      exclude 'META-INF/LGPL2.1'
    }

    dependencies {
        implementation "androidx.compose.ui:ui:$compose_version"
        implementation "androidx.compose.ui:ui-unit:$compose_version"
        implementation "androidx.compose.ui:ui-util:$compose_version"
        implementation "androidx.compose.foundation:foundation-layout:$compose_version"
        implementation "androidx.compose.foundation:foundation:$compose_version"
        implementation "androidx.compose.material:material:$compose_version"
        implementation project(':core')
        androidTestImplementation "org.jetbrains.kotlin:kotlin-test-junit:$kotlin_version"
        androidTestImplementation 'junit:junit:4.13.1'
        androidTestImplementation "androidx.compose.ui:ui-test:$compose_version"
        androidTestImplementation "androidx.compose.ui:ui-test-junit4:$compose_version"
        androidTestImplementation 'androidx.test:runner:1.3.0'
        androidTestImplementation 'androidx.test:rules:1.3.0'
        androidTestImplementation "androidx.activity:activity:1.1.0"
  }
}

configurations.all {
    resolutionStrategy.dependencySubstitution {
        substitute module("androidx.constraintlayout:constraintlayout-core") using project(":core") because "we work with the unreleased development version"
    }
}
 configurations.configureEach {
         resolutionStrategy.eachDependency { DependencyResolveDetails details ->
             if (details.requested.group == 'androidx.lifecycle' &&
                     details.requested.name != 'lifecycle-viewmodel-compose') {
                 details.useVersion('2.3.0-rc01')
             }
             if (details.requested.group == 'androidx.savedstate') {
                 details.useVersion('1.1.0-rc01')
             }
         }
 }

globalConfig {
    pomName = 'ConstraintLayout for Jetpack Compose'
    pomDescription = 'ConstraintLayout for Jetpack Compose'
}
